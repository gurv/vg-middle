import com.bmuschko.gradle.docker.tasks.image.*

plugins {
    id 'com.bmuschko.docker-remote-api' version '3.4.4' apply false
    id "org.sonarqube" version "2.7.1"
}

configure(rootProject) {
    group = 'io.github.gurv'
    version = '0.2.0-SNAPSHOT'

    ext {
        prometheusVersion = 'v2.8.1'
        prometheusWmiExporterVersion = '0.2.10'
        grafanaVersion = '5.1.4'
        h2Version = '2017-06-10'
        zookeeperVersion = '3.4.11'
        kafkaVersion = '1.1.0'
        confluentVersion = '4.1.3'
        elkVersion = '6.4.0'
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "gurv-github"
        property "sonar.projectVersion", "${project.version}"
        property "sonar.sources", "."
        property "sonar.links.ci", "https://travis-ci.org/gurv/vg-middle"
        property "sonar.links.scm", "https://github.com/gurv/vg-middle"
        property "sonar.links.issue", "https://github.com/gurv/vg-middle/issues"
    }
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: 'base'
    apply plugin: 'com.bmuschko.docker-remote-api'

    ext {
        imageName = "vg-${project.name}"
    }

    docker {
        registryCredentials {
            email = 'vladimir.gurinovich@gmail.com'
        }
    }

    task buildImage(type: DockerBuildImage) {
        inputDir = project.projectDir
        tag = project.imageName
    }

    task tagImageDockerHub(type: DockerTagImage) {
        dependsOn buildImage
        imageId = project.imageName
        repository = "${project.dockerhubRegistry}/${project.imageName}"
        tag = 'latest'
    }
    
    task pushImageToDockerHub(type: DockerPushImage) {
        dependsOn tagImageDockerHub
        imageName = "${project.dockerhubRegistry}/${project.imageName}"
        tag = 'latest'
        doFirst {
            registryCredentials.url = project.dockerhubRegistry
            registryCredentials.username = findProperty('dockerhubUsername') ?: System.getenv('CI_DOCKER_USERNAME')
            registryCredentials.password = findProperty('dockerhubPassword') ?: System.getenv('CI_DOCKER_PASSWORD')
        }
    }

    task tagImageGitHub(type: DockerTagImage) {
        dependsOn buildImage
        imageId = project.imageName
        repository = "${project.githubRegistry}/${project.imageName}"
        tag = "latest"
    }

    task pushImageToGitHub(type: DockerPushImage) {
        dependsOn tagImageGitHub
        imageName = "${project.githubRegistry}/${project.imageName}"
        tag = 'latest'
        doFirst {
            registryCredentials.url = project.githubRegistry
            registryCredentials.username = findProperty('githubUsername') ?: System.getenv('GITHUB_USERNAME')
            registryCredentials.password = findProperty('githubToken') ?: System.getenv('GITHUB_TOKEN')
            println "!!!1 ${registryCredentials.username}"
            println "!!!2 ${registryCredentials.password}"
        }
    }
}