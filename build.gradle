plugins {
    id 'com.bmuschko.docker-remote-api' version '3.2.4' apply false
    id "org.sonarqube" version "2.6.2" apply false
}

configure(rootProject) {
    group = 'ru.gurv.vg'
    version = '0.1.0'

    ext {
        prometheusVersion = 'v2.3.1'
        prometheusWmiExporterVersion = '0.2.10'
        grafanaVersion = '5.1.4'
        h2Version = '2017-06-10'
        zookeeperVersion = '3.4.11'
        kafkaVersion = '1.1.0'
        confluentVersion = '4.1.1'
        prometheusWmiExporter = findProperty('prometheusWmiExporter') ?: "C:/app/wmi_exporter-$prometheusWmiExporterVersion/wmi_exporter.exe"
    }
}

configure(subprojects) {
    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: 'base'
    apply plugin: 'org.sonarqube'
    apply plugin: 'com.bmuschko.docker-remote-api'

    ext {
        dockerImageName = "gurv/${rootProject.name}-${project.path.substring(1).replace(':', '-')}:latest"
        dockerContainerName = "${rootProject.name}-${project.path.substring(1).replace(':', '-')}"
        dockerImageBuildArgs = []
    }

    // https://github.com/bmuschko/gradle-docker-plugin/issues/467#issuecomment-353865846
    dependencies {
        dockerJava 'com.github.docker-java:docker-java:3.1.0-rc-2'
        dockerJava 'org.slf4j:slf4j-simple:1.7.5'
        dockerJava 'cglib:cglib:3.2.0'
    }
}